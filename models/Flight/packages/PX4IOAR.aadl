package PX4IOAR
public
  with UAS;
  with UAV;
  with DATATYPES;
  with PX4FMU;
  with CORTEX;
  with SMACCM;
  with motors_type;

  system PX4IOAR
    features
      expansion_connector: feature group inverse of PX4FMU::expansion_slot;
      motors_and_radios: feature group inverse of UAV::motors_and_radios;
      mavlink: in out data port DATATYPES::EncryptMAVLink;
      flight_control_commands: in data port DATATYPES::Flight_Control_Commands;
      rc_radio: requires bus access UAS::RF_Spectrum.RF_2_4GHz;
      x3dr_radio: requires bus access UAS::RF_Spectrum.RF_915MHz;
  end PX4IOAR;
  
  system implementation PX4IOAR.Impl
    subcomponents
      M: device Motors;
      
      BUZZER: device Buzzer;
      RC: device RC_Radio;
      x3DR: device x3DR_Radio;
      USART2: bus USART2;
    
    connections
      -- Motors
      conn0 : port motors_and_radios.motors -> M.commands;
      conn1 : bus access M.bus_access <-> USART2;
     
      -- Buzzer
      conn2 : bus access BUZZER.bus_access <-> expansion_connector.alarm;
      
      -- RC Radio
      conn3 : port RC.commands_internal -> motors_and_radios.flight_control_commands;
      conn4 : port flight_control_commands -> RC.commands_external;
      conn5 : bus access RC.ppm <-> expansion_connector.ppm;
      conn6 : bus access RC.antenna <-> rc_radio;
      
      -- 3DR Radio
      conn7 : port x3DR.mavlink_internal <-> motors_and_radios.mavlink;
      conn8 : port mavlink <-> x3DR.mavlink_external;
      conn9 : bus access x3DR.usart <-> expansion_connector.usart1;
      conn10 : bus access x3DR.antenna <-> x3dr_radio;
      
      
    annex resolute {**
    	prove execute_completeness(this.x3DR) 
    **};
      
  end PX4IOAR.Impl;
  
  device Motors
    features
      commands : in data port motors_type::motors.impl;
      --commands: in data port DATATYPES::Servo_Commands;
      bus_access: requires bus access USART2; 
  end Motors;
  
  device Buzzer
    features
      bus_access: requires bus access CORTEX::AHB_Bus_Matrix;
  end Buzzer;
  
  device RC_Radio
    features
      commands_internal: out data port DATATYPES::Flight_Control_Commands;
      commands_external: in data port DATATYPES::Flight_Control_Commands;
      ppm: requires bus access CORTEX::AHB_Bus_Matrix;
      antenna: requires bus access UAS::RF_Spectrum.RF_2_4GHz;
  end RC_Radio;
  
  device x3DR_Radio
    features
      mavlink_internal: in out data port DATATYPES::EncryptMAVLink;
      mavlink_external: in out data port DATATYPES::EncryptMAVLink;
      usart: requires bus access CORTEX::AHB_Bus_Matrix;
      antenna: requires bus access UAS::RF_Spectrum.RF_915Mhz;
      
    properties
      SMACCM::Always_Sends_Message => true; 
      
     -- annex resolute {**  
     -- 	prove execute_completeness(this)
     -- **};
      
  end x3DR_Radio;
  
  bus USART2
  end USART2;
end PX4IOAR;