package DOS_Resilient
public

annex resolute 
{**

is_dos_safe(x : component) <=
 ** x " is safe from a denial of service attack\n"
      " because either the radio thread is not active\n"
      " or nothing is connected to the radio thread\n" **
      radio_thread_is_not_active() or not_connected_to_radio(x)

radio_thread_is_not_active() <=
 ** "The radio thread is not active" **
 forall(t : thread) . is_not_radio(t)

not_connected_to_radio(x : component) <=
 ** x " cannot read from the radio" **
     forall (c : connection).
 	 forall (y : component).
 	     (connected(y, c, x) =>
 	     	is_not_radio(y) and not_connected_to_radio(y))

is_not_radio(x : component) <=
 ** x " is not the radio" **
     not class_of(x, SOFTWARE::Radio_Control)
     
**};

    
end DOS_Resilient;